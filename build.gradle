plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
	id 'org.hibernate.orm' version '7.2.0.Final' apply false
	id 'org.graalvm.buildtools.native' version '0.11.3' apply false
}

group = 'eu.dec21'
version = '0.0.1-SNAPSHOT'
description = 'Appointment Management System'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

def composeFile = file("${rootProject.projectDir}/compose.yaml")
// Docker Compose usage logic:
// - CI environments (detected by CI env var): false (use GitHub service containers or Testcontainers)
// - Local development: true by default (can be disabled with -PwithDocker=false or WITH_DOCKER=false)
def isCiEnvironment = System.getenv("CI") != null
def withDocker = !isCiEnvironment && 
                 (project.findProperty("withDocker") != "false") && 
                 (System.getenv("WITH_DOCKER") != "false")

tasks.register("composeUp", Exec) {
	group = "docker"
	description = "Start docker compose services"
	workingDir = rootProject.projectDir
	commandLine "docker", "compose", "-f", composeFile.absolutePath, "up", "-d"
}

tasks.register("composeDownNoFail", Exec) {
	group = "docker"
	description = "Stop docker compose services (ignore failures)"
	workingDir = rootProject.projectDir
	commandLine "docker", "compose", "-f", composeFile.absolutePath, "down", "-v"
	ignoreExitValue = true
}

// Cross-platform config validation
def isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
def syncScript = isWindows ? 
	"${rootProject.projectDir}/scripts/sync-configs.ps1" : 
	"${rootProject.projectDir}/scripts/sync-configs.sh"
def syncCommand = isWindows ? 
	["pwsh", "-ExecutionPolicy", "Bypass", "-File"] : 
	["bash"]

tasks.register("validateConfigs", Exec) {
	group = "verification"
	description = "Validates that all configuration files are synchronized across environments"
	workingDir = rootProject.projectDir
	commandLine = syncCommand + [syncScript]
	ignoreExitValue = true
}

tasks.register("syncConfigsHelp", Exec) {
	group = "verification"
	description = "Shows detailed instructions for synchronizing configuration files"
	workingDir = rootProject.projectDir
	commandLine = syncCommand + [syncScript, isWindows ? "-Fix" : "--fix"]
	ignoreExitValue = true
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.graalvm.buildtools.native'

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom libs.spring.cloud.bom.get().toString()
			mavenBom libs.testcontainers.bom.get().toString()
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		compileOnly "org.projectlombok:lombok:1.18.42"
		annotationProcessor "org.projectlombok:lombok:1.18.42"

		implementation 'org.springframework.boot:spring-boot-starter-security'
		implementation 'org.springframework.boot:spring-boot-starter-mail'
		
		// Testcontainers for integration tests
		testImplementation libs.testcontainers.junit.jupiter
		testImplementation libs.testcontainers.postgresql
		testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	}

	tasks.withType(Test).configureEach {
		useJUnitPlatform()

		if (withDocker) {
			dependsOn(rootProject.tasks.named("composeUp"))
			finalizedBy(rootProject.tasks.named("composeDownNoFail"))
		}
	}

	tasks.matching { it.name == "bootRun" }.configureEach {
		if (withDocker) {
			dependsOn(rootProject.tasks.named("composeUp"))
			// Note: We don't finalize with composeDown for bootRun
			// This allows Docker to keep running during manual testing/development
			// To stop Docker manually: ./gradlew composeDownNoFail
		}
	}
}

def springModules = subprojects.findAll { it.name !in ['common', 'exceptions'] }

configure(springModules) {
	dependencies {
		implementation(libs.springdoc.openapi.starter.webmvc.ui)

		implementation project(':common')
		implementation project(':exceptions')

		implementation "org.springframework.boot:spring-boot-starter"
//		implementation 'org.springframework.boot:spring-boot-micrometer-tracing-brave'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
//		implementation 'org.springframework.boot:spring-boot-starter-opentelemetry'
		implementation 'org.springframework.boot:spring-boot-starter-restclient'
////		implementation 'org.springframework.boot:spring-boot-starter-security'
		implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
		implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server'
		implementation 'org.springframework.boot:spring-boot-starter-webmvc'

		implementation 'org.springframework.boot:spring-boot-starter-validation'
//		implementation 'io.micrometer:micrometer-tracing-bridge-brave'
////		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
//		runtimeOnly 'io.micrometer:micrometer-registry-otlp'
//		testImplementation 'org.springframework.boot:spring-boot-micrometer-tracing-test'
		testImplementation 'org.springframework.boot:spring-boot-starter-opentelemetry-test'
		testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
		testImplementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client-test'
		testImplementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server-test'
		testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
		testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}
}
