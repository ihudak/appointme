plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
	id 'org.hibernate.orm' version '7.2.0.Final' apply false
	id 'org.graalvm.buildtools.native' version '0.11.3' apply false
}

group = 'eu.dec21'
version = '0.0.1-SNAPSHOT'
description = 'Appointment Management System'

def composeFile = file("${rootProject.projectDir}/compose.yaml")

tasks.register("composeUp", Exec) {
	group = "docker"
	description = "Start docker compose services"
	workingDir = rootProject.projectDir
	commandLine "docker", "compose", "-f", composeFile.absolutePath, "up", "-d"
}

tasks.register("composeDownNoFail", Exec) {
	group = "docker"
	description = "Stop docker compose services (ignore failures)"
	workingDir = rootProject.projectDir
	commandLine "docker", "compose", "-f", composeFile.absolutePath, "down", "-v"
	ignoreExitValue = true
}

subprojects {
	apply plugin: 'java'

	repositories {
		mavenCentral()
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(25)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	tasks.withType(Test).configureEach {
		useJUnitPlatform()
		dependsOn(rootProject.tasks.named("composeUp"))
		finalizedBy(rootProject.tasks.named("composeDownNoFail"))
	}

	tasks.matching { it.name == "bootRun" }.configureEach {
		dependsOn(rootProject.tasks.named("composeUp"))
		finalizedBy(rootProject.tasks.named("composeDownNoFail"))
	}
}

def springModules = subprojects.findAll { it.name !in ['common', 'exceptions'] }

configure(springModules) {
	apply plugin: 'org.springframework.boot'

	dependencies {
		implementation platform("org.springframework.boot:spring-boot-dependencies:4.0.1")
		implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1"

		implementation "org.springframework.boot:spring-boot-starter"
		implementation "org.springframework.boot:spring-boot-starter-security"
		implementation "org.springframework.boot:spring-boot-starter-actuator"
		implementation "org.springframework.boot:spring-boot-starter-webmvc"
		implementation "org.springframework.boot:spring-boot-starter-restclient"
		implementation "org.springframework.boot:spring-boot-starter-mail"
		implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
		implementation "org.springframework.boot:spring-boot-starter-security-oauth2-client"
		implementation "org.springframework.boot:spring-boot-starter-security-oauth2-resource-server"
		implementation "org.springframework.boot:spring-boot-starter-opentelemetry"
		implementation "org.springframework.boot:spring-boot-micrometer-tracing-brave"
		implementation "io.micrometer:micrometer-tracing-bridge-brave"

		compileOnly "org.projectlombok:lombok:1.18.42"
		annotationProcessor "org.projectlombok:lombok:1.18.42"
	}
}
